buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    /* APACHE HTTP - deprecated */
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "mobiric.fhbsc.weather"
        minSdkVersion 17
        targetSdkVersion 22

        versionCode 7
        versionName "1.1.0"

        // allow Crashlytics configuration based on build type - disable this in debug builds
        buildConfigField("boolean", "CRASHLYTICS", "true")

        /*BETA DISTRIBUTION*/
        ext.betaDistributionReleaseNotesFilePath = "extras/fabric/beta/release_notes.txt"
        ext.betaDistributionEmailsFilePath = "extras/fabric/beta/distribution.txt"
    }

    signingConfigs {
        release {
            // keystore properties stored in an external file - do not commit to repo
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            buildConfigField("boolean", "CRASHLYTICS", "false")
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

}

dependencies {
    /* Support Libs */
    compile "com.android.support:support-core-utils:24.2.0"
    compile 'com.android.support:appcompat-v7:24.2.0'

    /* GSON */
    compile 'com.google.code.gson:gson:2.4'

    /* Crashlytics */
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
}
